#include "WiFi.h"
#include <PubSubClient.h>

#define DASHBOARD_MQTT_SERVER               "broker.hivemq.com"

#define DASHBOARD_MQTT_PORT                 1883
#define DASHBOARD_MQTT_USER                 "test"
#define DASHBOARD_MQTT_PASSWORD             "123456"

#define DASHBOARD_TOPIC_TEMPERATURE         "MySensor/Temperature"
#define DASHBOARD_TOPIC_HUMIDITY            "MySensor/Humidity"
#define DASHBOARD_TOPIC_LIGHT_1             "MySensor/Light1"
#define DASHBOARD_TOPIC_LIGHT_2             "MySensor/Light2"
#define DASHBOARD_TOPIC_SOIL                "MySensor/Soil"

#define DASHBOARD_TOPIC_LED                 "MyLed/Control"
#define DASHBOARD_TOPIC_THRESHOLD_RAIN      "MySensor/Threshold/Rain"
#define DASHBOARD_TOPIC_THRESHOLD_LIGHT     "MySensor/Threshold/Light"
#define DASHBOARD_TOPIC_THRESHOLD_SOIL      "MySensor/Threshold/Soil"
#define DASHBOARD_TOPIC_MODE                "MyRelay/Mode"
#define DASHBOARD_TOPIC_RELAY               "MyRelay/Control"
#define DASHBOARD_TOPIC_TEST                "MyTest/Check"

#define DASHBOARD_TOPIC_SENSOR              "MySensor/Value"

#define DASHBOARD_BUFFER_LENGTH 7
#define MESSAGE_START                       ('(')
#define MESSAGE_END                         (')')

WiFiClient wifiClient;
PubSubClient client(wifiClient);

bool g_receiveFlag = false;
uint8_t g_readData;
uint8_t g_bufferRx[DASHBOARD_BUFFER_LENGTH];
uint8_t g_idx = 0;

const char* g_ssid = "Quang";
const char* g_password = "27112000";
int g_updateTime = 0;
uint8_t g_ledSta = 0;

void setup(void)
{
    Serial.begin(9600);
    Serial.setTimeout(500);
    pinMode(2, OUTPUT);

    // Connect to WiFi
    WiFi.begin(g_ssid, g_password);
    while (WiFi.status() != WL_CONNECTED)
    {
        g_ledSta = !g_ledSta;
        digitalWrite(2, g_ledSta);
        delay(100);
    }
    client.setServer(DASHBOARD_MQTT_SERVER, DASHBOARD_MQTT_PORT);
    client.setCallback(Dashboard_Callback);
    DashBoard_ConnectToBroker();
}

void loop()
{
    client.loop();
    if (!client.connected()) {
        DashBoard_ConnectToBroker();
    }
    Dashboard_ReceiveDataFromSTM32();
}

void Dashboard_ReceiveDataFromSTM32(void)
{
    if (Serial.available() > 0)
    {
        g_readData = Serial.read();
        if (g_readData == MESSAGE_START && g_receiveFlag == false)
        {
            g_receiveFlag = true;
            g_idx = 0;
            g_bufferRx[DASHBOARD_BUFFER_LENGTH - 1] = '\0';
        }
        else
        {
            if (g_receiveFlag == true)
            {
                if (g_idx < DASHBOARD_BUFFER_LENGTH)
                {
                    g_bufferRx[g_idx++] = g_readData;
                }
                else if (g_readData == MESSAGE_END || g_idx == DASHBOARD_BUFFER_LENGTH)
                {
                    g_bufferRx[DASHBOARD_BUFFER_LENGTH - 1] = '\0';
                    Dashboard_HandleDataFromSTM32();
                    g_receiveFlag = false;
                    g_idx = 0;
                }
            }
        }
    }
}
uint8_t checksumVal = 0;
char tem1[4];
char hum1[4];
char light1[4];
char light2[4];
char soil[4];
char hum2[4];
char sensor[9];
void Dashboard_HandleDataFromSTM32(void)
{

    checksumVal = g_bufferRx[0] ^ g_bufferRx[1] ^ g_bufferRx[2] ^ g_bufferRx[3] ^ g_bufferRx[4];
    if (checksumVal == g_bufferRx[5])
    {
        sprintf(tem1, "%d", g_bufferRx[0]);
        client.publish(DASHBOARD_TOPIC_TEMPERATURE, tem1, true);
        sprintf(hum1, "%d", g_bufferRx[1]);
        client.publish(DASHBOARD_TOPIC_HUMIDITY, hum1, true);

        sprintf(light1, "%d", g_bufferRx[2]);
        client.publish(DASHBOARD_TOPIC_LIGHT_1, light1, true);

        sprintf(light2, "%d", g_bufferRx[3]);
        client.publish(DASHBOARD_TOPIC_LIGHT_2, light2, true);

        sprintf(soil, "%d", g_bufferRx[4]);
        client.publish(DASHBOARD_TOPIC_SOIL, soil, true);

        sprintf(sensor, "%d/%d/%d/%d/%d", g_bufferRx[0], g_bufferRx[1], g_bufferRx[2], g_bufferRx[3], g_bufferRx[4]);
        client.publish(DASHBOARD_TOPIC_SENSOR, sensor, true);
    }
}

void DashBoard_ConnectToBroker() {
    while (!client.connected()) {
        String clientId = "ESP32";
        clientId += String(random(0xffff), HEX);
        if (client.connect(clientId.c_str(), DASHBOARD_MQTT_USER, DASHBOARD_MQTT_PASSWORD)) {
            client.subscribe(DASHBOARD_TOPIC_LED);
            client.subscribe(DASHBOARD_TOPIC_THRESHOLD_RAIN);
            client.subscribe(DASHBOARD_TOPIC_THRESHOLD_LIGHT);
            client.subscribe(DASHBOARD_TOPIC_THRESHOLD_SOIL);
            client.subscribe(DASHBOARD_TOPIC_MODE);
            client.subscribe(DASHBOARD_TOPIC_RELAY);
            digitalWrite(16, HIGH);
        } else {
            g_ledSta = !g_ledSta;
            digitalWrite(16, g_ledSta);
            delay(1000);
        }
    }
}

bool g_mode = false;
void Dashboard_Callback(char* topic, byte *payload, unsigned int length) {
    char statusMsg[length + 1];
    memcpy(statusMsg, payload, length);
    statusMsg[length] = NULL;
    String topicMsg(statusMsg);

    if (String(topic) == DASHBOARD_TOPIC_LED)
    {
        if (topicMsg == "on")
        {
            Serial.println("D1");
        }
        else if (topicMsg == "off")
        {
            Serial.println("D0");
        }
    }
    if (String(topic) == DASHBOARD_TOPIC_THRESHOLD_RAIN)
    {
        Serial.println("R" +topicMsg);
    }
    else if (String(topic) == DASHBOARD_TOPIC_THRESHOLD_LIGHT)
    {
        Serial.println("L" + topicMsg);
    }
    else if (String(topic) == DASHBOARD_TOPIC_THRESHOLD_SOIL)
    {
        Serial.println("S" + topicMsg);
    }
    else if (String(topic) == DASHBOARD_TOPIC_MODE)
    {
        if (topicMsg == "true")
        {
            g_mode = true;
            Serial.println("M1");
        }
        else if (topicMsg == "false")
        {
            g_mode = false;
            Serial.println("M0");
        }
    }
    else if (String(topic) == DASHBOARD_TOPIC_RELAY)
    {
        if (g_mode == true)
        {
          if (topicMsg == "on")
          {
              Serial.println("E1");
          }
          else if (topicMsg == "off")
          {
              Serial.println("E0");
          }
        }
    }
}
